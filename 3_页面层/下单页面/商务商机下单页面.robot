*** Settings ***
Resource          ../../5_SeleniumLibrary扩展层/公共关键字扩展.robot
Resource          ../../4_交互层/订单/商务商机下单交互.robot

*** Keywords ***
进入商务商机下单
    点击头部菜单    订单
    点击一级菜单    订单中心
    点击二级菜单    订单管理
    点击三级菜单    商务商机下单
    商务商机下单交互.切换商务商机下单iframe

先款后单第一步-纸质合同
    [Arguments]    ${客户名称/商机所属人}    ${商机业态}    ${客户信息-客户名称}    ${商品名称}    ${模糊属性值}    ${产品金额}
    ...    ${纸质合同编号}    ${生产所属事业部}
    商务商机下单页面.私有_选择商机    ${客户名称/商机所属人}    ${商机业态}
    商务商机下单页面.私有_输入信息    ${客户信息-客户名称}    ${商品名称}    ${模糊属性值}    ${产品金额}    ${生产所属事业部}
    商务商机下单交互.输入纸质合同编号    ${纸质合同编号}
    商务商机下单交互.选择付款方式    先款后单
    商务商机下单交互.点击下单

先款后单第一步-电子合同
    [Arguments]    ${客户名称/商机所属人}    ${商机业态}    ${客户信息-客户名称}    ${商品名称}    ${模糊属性值}    ${产品金额}
    ...    ${生产所属事业部}    ${index}=0    # 电子合同默认选择列表第一个
    商务商机下单页面.私有_选择商机    ${客户名称/商机所属人}    ${商机业态}
    商务商机下单页面.私有_输入信息    ${客户信息-客户名称}    ${商品名称}    ${模糊属性值}    ${产品金额}    ${生产所属事业部}
    商务商机下单交互.选择电子合同    ${index}
    商务商机下单交互.选择付款方式    先款后单
    商务商机下单交互.点击下单

先单后款第一步-纸质合同
    [Arguments]    ${客户名称/商机所属人}    ${商机业态}    ${审核人}    ${客户信息-客户名称}    ${商品名称}    ${模糊属性值}
    ...    ${产品金额}    ${纸质合同编号}    ${生产所属事业部}
    商务商机下单页面.私有_选择商机    ${客户名称/商机所属人}    ${商机业态}
    商务商机下单页面.私有_输入信息    ${客户信息-客户名称}    ${商品名称}    ${模糊属性值}    ${产品金额}    ${生产所属事业部}
    商务商机下单交互.输入纸质合同编号    ${纸质合同编号}
    商务商机下单交互.选择审核人    ${审核人}
    商务商机下单交互.点击下单

先单后款第一步-电子合同
    [Arguments]    ${客户名称/商机所属人}    ${商机业态}    ${审核人}    ${客户信息-客户名称}    ${商品名称}    ${模糊属性值}
    ...    ${产品金额}    ${生产所属事业部}    ${index}=0    # 电子合同默认选择列表第一个
    商务商机下单页面.私有_选择商机    ${客户名称/商机所属人}    ${商机业态}
    商务商机下单页面.私有_输入信息    ${客户信息-客户名称}    ${商品名称}    ${模糊属性值}    ${产品金额}    ${生产所属事业部}
    商务商机下单交互.选择电子合同    ${index}
    商务商机下单交互.选择审核人    ${审核人}
    商务商机下单交互.点击下单

下单第二步-不选择顾问-代理记账
    [Arguments]    ${公司名称}    ${统一社会信用代码}    ${起始时间}    ${结束时间}
    商务商机下单页面.私有_第二步下单-代理记账    ${公司名称}    ${统一社会信用代码}    ${起始时间}    ${结束时间}
    商务商机下单交互.确认订单-暂不选择顾问
    ${订单号}=    商务商机下单页面.私有_获取订单号
    [Return]    ${订单号}

下单第二步-指定顾问-代理记账
    [Arguments]    ${公司名称}    ${统一社会信用代码}    ${顾问名称}    ${起始时间}    ${结束时间}
    商务商机下单页面.私有_第二步下单-代理记账    ${公司名称}    ${统一社会信用代码}    ${起始时间}    ${结束时间}
    商务商机下单交互.确认订单-选择顾问    ${顾问名称}
    ${订单号}=    商务商机下单页面.私有_获取订单号
    [Return]    ${订单号}

下单第二步-附属业务
    [Arguments]    ${公司名称}    ${统一社会信用代码}
    商务商机下单页面.私有_第二步下单-附属业务    ${公司名称}    ${统一社会信用代码}
    商务商机下单交互.确定下单-点击确定
    ${订单号}=    商务商机下单页面.私有_获取订单号
    [Return]    ${订单号}

私有_选择商机
    [Arguments]    ${客户名称/商机所属人}    ${商机业态}
    商务商机下单交互.点击选择商机
    商务商机下单交互.切换选择商机iframe
    商务商机下单交互.搜索商机    ${客户名称/商机所属人}
    商务商机下单交互.选择商机    ${商机业态}

私有_输入信息
    [Arguments]    ${客户信息-客户名称}    ${商品名称}    ${模糊属性值}    ${产品金额}    ${生产所属事业部}
    Sleep    2
    商务商机下单交互.输入客户名称    ${客户信息-客户名称}
    商务商机下单交互.选择客户角色
    商务商机下单交互.选择业务地区
    商务商机下单交互.点击添加产品
    商务商机下单交互.切换选择产品iframe
    商务商机下单交互.选择商品    ${商品名称}    ${模糊属性值}
    商务商机下单交互.输入产品金额    ${产品金额}
    商务商机下单交互.选择生产所属事业部    ${生产所属事业部}

私有_第二步下单-代理记账
    [Arguments]    ${公司名称}    ${统一社会信用代码}    ${起始时间}    ${结束时间}
    商务商机下单交互.切换到下单第二步iframe
    商务商机下单交互.选择客户类型
    商务商机下单交互.输入公司名称    ${公司名称}
    商务商机下单交互.输入统一社会信用代码    ${统一社会信用代码}
    商务商机下单交互.地区选择
    商务商机下单交互.行业选择
    商务商机下单交互.纳税性质选择
    商务商机下单交互.合同起始日期选择    ${起始时间}    ${结束时间}
    商务商机下单交互.第二步-点击下单

私有_第二步下单-附属业务
    [Arguments]    ${公司名称}    ${统一社会信用代码}
    商务商机下单交互.切换到下单第二步iframe
    商务商机下单交互.输入公司名称    ${公司名称}
    商务商机下单交互.输入统一社会信用代码    ${统一社会信用代码}
    商务商机下单交互.地区选择
    商务商机下单交互.行业选择
    商务商机下单交互.纳税性质选择
    商务商机下单交互.第二步-点击下单

私有_获取订单号
    商务商机下单交互.断言成功
    ${订单号}=    商务商机下单交互.获取订单号
    Sleep    5
    [Return]    ${订单号}
